group 'CS4341'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'

targetCompatibility = 1.8

task run(type: JavaExec) {
    main = 'connectn.Main'
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
}

task runref << {
    File refJar = file('Referee.jar')
    File aiJar = file('build/libs/ConnectN-1.0-SNAPSHOT.jar')
    File debugJar = file('build/libs/ConnectN-DEBUG-1.0-SNAPSHOT.jar')

    ProcessBuilder pb = new ProcessBuilder("java",
            "-jar", refJar.absolutePath,
            "java -jar " + aiJar.absolutePath + "",
            "java -jar " + debugJar.absolutePath + "",
            "6", "7", "4", "10", "120"
    )

    pb.redirectOutput(ProcessBuilder.Redirect.INHERIT)
    pb.redirectError(ProcessBuilder.Redirect.INHERIT)
    pb.start().waitFor();
}
runref.dependsOn {
    tasks.findAll { task -> task.name.startsWith('dist') }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

task dist(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                'Main-Class': 'connectn.Main'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    with jar
}

task distdebug(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                'Main-Class': 'debugplayer.DebugPlayerMain'
    }
    baseName = project.name + '-DEBUG'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    with jar
}

